.SECTION .DATA
	FRAME_WIDTH: .WORD 0620
	FRAME_HEIGHT: .WORD 0454
	FRAME_WIDTH_BYTES: .WORD 0144
	FRAME_BUFFER_OFFSET: .WORD 0040000
	FRAME_BUFFER_SIZE: .WORD 0072460
	SYMBOL_IMAGE_SIZE: .WORD 020
	FONT_ADDRESS: .WORD 0154600
	KEYBOARD_TO_FONT_TABLE_ADDRESS: .WORD 0157634
	# CONSOLE_X_ADDR: .WORD 0001000
	# CONSOLE_Y_ADDR: .WORD 0001002
	# KEYBOARD_SYMBOL_REGISTER: .WORD 0176000
	# KEYBOARD_INT_PC_REGISTER: .WORD 0176002
	# KEYBOARD_INT_PSW_REGISTER: .WORD 0176004
	FLAG1: .WORD 0xDEAD
	FLAG2: .WORD 0xBEAF

.SECTION .CODE
.GLOBAL MAIN

# /-------------------------------------------------------\
# | MAIN ROM FUNCTION                                     |
# \-------------------------------------------------------/
MAIN:
	# Configure keyboard interrupt handler
	MOV KEYBOARD_PUSH_BUTTON_EVENT_HANDLER, (0176002)
	MOV 0000000, (0176004)
	
	# Configure echo console
	JSR R5, CLEAR_SCREEN_FUNCTION
ECHO_LOOP:
	WAIT
	BR ECHO_LOOP

	HALT

# /-------------------------------------------------------\
# | KEYBOARD PUSH BUTTON ECHO HANDLER                     |
# \-------------------------------------------------------/
KEYBOARD_PUSH_BUTTON_EVENT_HANDLER:
	BIT 0001000, (0176000)
	BNE KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_RENDER
	RTI

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_RENDER:
	# Enter pressed
	CMP 0001000, (0176000)
	BEQ KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_NEW_LINE
	CMP 0001001, (0176000)
	BEQ KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE

	# Alloc arguments memory
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000000, -(R6)
	# char_code: SP+0
	# y: SP+2
	# x: SP+4

	# Configure char_code argument
	MOV (0176000), 0(R6)
	BIC 0001000, 0(R6)
	JSR R5, KEYBOARD_TO_FONT_CONVERT_FUNCTION

	JSR R5, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6

	ADD 010, (0001000)
	CMP (0001000), (FRAME_WIDTH)
	BNE KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_NEW_LINE:
	MOV 0000000,  (0001000)
	ADD 010, (0001002)

	# Print > symbol at start of the line
	JSR R5, PRINT_GREETING_SYMBOL_FUNCTION
	
	CMP (0001002), (FRAME_HEIGHT)
	BLT KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END
	JSR R5, CLEAR_SCREEN_FUNCTION

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END:
	RTI

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE:
	ADD -010, (0001000)

	CMP 010, (0001000)
	BLT KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE1
	# Left clamp
	MOV 010, (0001000)

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE1:
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000136, -(R6)

	JSR R5, PRINT_SCREEN_CHAR_FUNCTION

	ADD 6, R6

	RTI

# /-------------------------------------------------------\
# | PRINT GREETING SYMBOL FUNCTION                        |
# \-------------------------------------------------------/
PRINT_GREETING_SYMBOL_FUNCTION:
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000105, -(R6)
	JSR R5, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6
	ADD 010, (0001000)
	RST R5

# /-------------------------------------------------------\
# | CLEAR SCREEN FUNCTION                                 |
# \-------------------------------------------------------/
CLEAR_SCREEN_FUNCTION:
	MOV (FRAME_BUFFER_OFFSET), -(R6)
	MOV 0000000, -(R6)

CLEAR_SCREEN_FUNCTION_LOOP:
	CMP 0(R6), FRAME_BUFFER_SIZE
	BLT CLEAR_SCREEN_FUNCTION_END
	MOV 0000000, @2(R6)
	
	ADD 2, 2(R6)
	ADD 2, 0(R6)
	
	BR CLEAR_SCREEN_FUNCTION_LOOP

CLEAR_SCREEN_FUNCTION_END:
	ADD 4, R6
	MOV 0000000, (0001000)
	MOV 0000000, (0001002)

	JSR R5, PRINT_GREETING_SYMBOL_FUNCTION
	RST R5


# /-------------------------------------------------------\
# | CONVERT KEYBOARD CODE TO FONT CODE FUNCTION           |
# \-------------------------------------------------------/
KEYBOARD_TO_FONT_CONVERT_FUNCTION:
	# Operands: keycode SP+2
	# Result: fontcode SP+2
	BIT 0000100, 2(R6)
	BEQ KEYBOARD_TO_FONT_CONVERT_LOWER

	BIC 0177700, 2(R6)
	ASL 2(R6)
	ADD (KEYBOARD_TO_FONT_TABLE_ADDRESS), 2(R6)
	MOV @2(R6), 2(R6)
	SWAB 2(R6)
	BIC 0177400, 2(R6)
	RST R5

KEYBOARD_TO_FONT_CONVERT_LOWER:
	BIC 0177700, 2(R6)
	ASL 2(R6)
	ADD (KEYBOARD_TO_FONT_TABLE_ADDRESS), 2(R6)
	MOV @2(R6), 2(R6)
	BIC 0177400, 2(R6)
	RST R5

# /-------------------------------------------------------\
# | SCREEN SYMBOL BLINK FUNCTION                          |
# \-------------------------------------------------------/
SCREEN_SYMBOL_BLINK_FUNCTION:
	# Operands: x, y
	# Old R5 value: SP+0
	# y: SP+2
	# x: SP+4

	# Additional Variable
	# tmp: SP+0
	# vram_line_offset: SP+2

	# vram_line_offset
	MOV (FRAME_BUFFER_OFFSET), -(R6)
	# tmp
	MOV 0000000, -(R6)

	# Old R5 value: SP+4
	# y: SP+6
	# x: SP+8

	ASR 8(R6)
	ASR 8(R6)
	ADD 8(R6), 2(R6)

SCREEN_SYMBOL_BLINK_H_OFFSET_LOOP:
	CMP 0(R6), 6(R6)
	BEQ SCREEN_SYMBOL_BLINK_RENDER
	ADD (FRAME_WIDTH_BYTES), 2(R6)
	INC 0(R6)
	BR SCREEN_SYMBOL_BLINK_H_OFFSET_LOOP

SCREEN_SYMBOL_BLINK_RENDER:
	CLR 0(R6)

SCREEN_SYMBOL_BLINK_RENDER_LOOP:
	CMP 010, 0(R6)
	BEQ SCREN_SYMBOL_BLINK_END
	COM @2(R6)
	ADD (FRAME_WIDTH_BYTES), 2(R6)
	INC 0(R6)
	BR SCREEN_SYMBOL_BLINK_RENDER_LOOP

SCREN_SYMBOL_BLINK_END:
	ADD 4, R6
	RST R5


# /-------------------------------------------------------\
# | PRINT SCREEN CHARACTER FUNCTION                       |
# \-------------------------------------------------------/
PRINT_SCREEN_CHAR_FUNCTION:
	# Operands: x, y, char_code
	# Old R5 value: SP+0
	# char_code: SP+2
	# y: SP+4
	# x: SP+6

	# line_printed
	MOV 0000000, -(R6)
	# vram_line_offset
	MOV (FRAME_BUFFER_OFFSET), -(R6)
	# tmp
	MOV 0000000, -(R6)
	ASR 12(R6)
	ASR 12(R6)
	ADD 12(R6), 2(R6)
	
	# New stack state:
	# tmp: SP+0
	# vram_line_offset: SP+2
	# line_printed: SP+4
	# Old Reg value: SP+6
	# char_code: SP+8
	# y: SP+10
	# x: SP+12
		
PRINT_SCREEN_CHAR_H_OFFSET_LOOP:
	CMP 0(R6), 10(R6)
	BEQ PRINT_SCREEN_CHAR_LOAD_SYMBOL
	ADD (FRAME_WIDTH_BYTES), 2(R6)
	INC 0(R6)
	BR PRINT_SCREEN_CHAR_H_OFFSET_LOOP

PRINT_SCREEN_CHAR_LOAD_SYMBOL:
	MOV (FONT_ADDRESS), 0(R6)

PRINT_SCREEN_CHAR_LOAD_SYMBOL_LOOP:
	CMP 4(R6), 8(R6)
	BEQ PRINT_SCREEN_CHAR_RENDERING
	ADD (SYMBOL_IMAGE_SIZE), 0(R6)
	INC 4(R6)
	BR PRINT_SCREEN_CHAR_LOAD_SYMBOL_LOOP

PRINT_SCREEN_CHAR_RENDERING:
	CLR 4(R6)

PRINT_SCREEN_CHAR_RENDERING_LOOP:
	CMP 4(R6), 8
	BEQ PRINT_SCREEN_CHAR_END
	MOV @0(R6), @2(R6)
	ADD (FRAME_WIDTH_BYTES), 2(R6)
	INC 4(R6)
	INC 0(R6)
	INC 0(R6)
	BR PRINT_SCREEN_CHAR_RENDERING_LOOP

PRINT_SCREEN_CHAR_END:
	ADD 6, R6
	RST R5
