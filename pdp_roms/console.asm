.SECTION .DATA
	FRAME_WIDTH: .WORD 0620
	FRAME_HEIGHT: .WORD 0454
	FRAME_WIDTH_BYTES: .WORD 0144
	FRAME_BUFFER_OFFSET: .WORD 0040000
	FRAME_BUFFER_SIZE: .WORD 0072460
	SYMBOL_IMAGE_SIZE: .WORD 020
	FONT_ADDRESS: .WORD 0154600
	KEYBOARD_TO_FONT_TABLE_ADDRESS: .WORD 0157634
	HELP_MESSAGE_ADDRESS: .WORD 0154100
	LOGO_ADDRESS: .WORD 0142270
	
	HALT_COMMAND_MASK: .WORD 0000007
	HALT_COMMAND_MASK1: .WORD 0000000
	HALT_COMMAND_MASK2: .WORD 0000013
	HALT_COMMAND_MASK3: .WORD 0000023

	HELP_COMMAND_MASK: .WORD 0000007
	HELP_COMMAND_MASK1: .WORD 0000004
	HELP_COMMAND_MASK2: .WORD 0000013
	HELP_COMMAND_MASK3: .WORD 0000017

	LOGO_COMMAND_MASK: .WORD 0000013
	LOGO_COMMAND_MASK1: .WORD 0000016
	LOGO_COMMAND_MASK2: .WORD 0000006
	LOGO_COMMAND_MASK3: .WORD 0000016

	CLR_COMMAND_MASK: .WORD 0000002
	CLR_COMMAND_MASK1: .WORD 0000013
	CLR_COMMAND_MASK2: .WORD 0000021

	FLAG1: .WORD 0xDEAD
	FLAG2: .WORD 0xBEAF
# Global variables addresses:
# Carriage_x_pos: 		0001000
# Carriage_y_pos: 		0001002
# Line_length: 			0001004
# Line_buffer_pointer: 	0001006
# Line_buffer_start: 	0001010

.SECTION .CODE
.GLOBAL MAIN

# /-------------------------------------------------------\
# | MAIN ROM FUNCTION                                     |
# \-------------------------------------------------------/
MAIN:
	# Configure keyboard interrupt handler
	MOV KEYBOARD_PUSH_BUTTON_EVENT_HANDLER, (0176002)
	MOV 0000340, (0176004)
	
	# Configure echo console
	JSR R7, CLEAR_SCREEN_FUNCTION
	JSR R7, HELP_COMMAND_FUNCTION
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
ECHO_LOOP:
	WAIT
	BR ECHO_LOOP

	HALT

# /-------------------------------------------------------\
# | KEYBOARD PUSH BUTTON ECHO HANDLER                     |
# \-------------------------------------------------------/
KEYBOARD_PUSH_BUTTON_EVENT_HANDLER:
	BIT 0001000, (0176000)
	BNE KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_RENDER
	RTI

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_RENDER:
	# Save registers R0-R2
	MOV R0, -(R6)
	MOV R1, -(R6)
	MOV R2, -(R6)

	# Enter pressed
	CMP 0001000, (0176000)
	BEQ KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_NEW_LINE
	CMP 0001001, (0176000)
	BEQ KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE

	# Alloc arguments memory
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000000, -(R6)
	# char_code: SP+0
	# y: SP+2
	# x: SP+4

	# Configure char_code argument
	# In-place code convertion
	MOV (0176000), 0(R6)
	BIC 0001000, 0(R6)
	JSR R7, KEYBOARD_TO_FONT_CONVERT_FUNCTION

	# Increment line length
	INC (0001004)
	
	# Move character to the line buffer
	MOV (0001006), R0
	MOV 0(R6), R0+
	MOV R0, (0001006)

	JSR R7, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6

	ADD 010, (0001000)
	CMP (0001000), (FRAME_WIDTH)
	BNE KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END

	# Move carriage to next line
	MOV 0000000, (0001000)
	ADD 012, (0001002)
	CMP (0001002), (FRAME_HEIGHT)
	BLT KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END
	JSR R7, CLEAR_SCREEN_FUNCTION
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
	BR KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_NEW_LINE:
	MOV 0000000, (0001000)
	ADD 012, (0001002)

	JSR R7, CHECK_COMMAND_FUNCTION
	
	MOV 0000000, (0001004)
	MOV 0001010, (0001006)

	# Print > symbol at start of the line
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
	
	CMP (0001002), (FRAME_HEIGHT)
	BLT KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END
	JSR R7, CLEAR_SCREEN_FUNCTION
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
	BR KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE:
	CMP 000, (0001004)
	BEQ KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END


	# Increment line length
	DEC (0001004)
	ADD -2, (0001006)

	ADD -010, (0001000)
	BPL KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE1

	MOV (FRAME_WIDTH), R0
	ADD -010, R0
	MOV R0, (0001000)
	ADD -012, (0001002)

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_BACKSPACE1:
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000136, -(R6)
	JSR R7, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6

KEYBOARD_PUSH_BUTTON_EVENT_HANDLER_END:
	# Restore registers R0-R2
	MOV (R6)+, R2
	MOV (R6)+, R1
	MOV (R6)+, R0

	RTI

# /-------------------------------------------------------\
# | CHECK COMMAND IN BUFFER FUNCTION                      |
# \-------------------------------------------------------/

CHECK_COMMAND_FUNCTION:
	# Save registers R0-R2
	MOV R0, -(R6)
	MOV R1, -(R6)
	MOV R2, -(R6)

CHECK_HALT_COMMAND:
	# HALT command check
	CMP 04, (0001004)
	BNE CHECK_HELP_COMMAND
	MOV HALT_COMMAND_MASK, R0
	MOV 0001010, R1
	CLR R2

CHECK_HALT_COMMAND_LOOP:
	CMP (R0)+, (R1)+
	BNE CHECK_HELP_COMMAND
	INC R2
	CMP 04, R2
	BNE CHECK_HALT_COMMAND_LOOP
	JSR R7, HALT_COMMAND_FUNCTION
	BR CHECK_COMMAND_FUNCTION_END

CHECK_HELP_COMMAND:
	# HELP command check
	CMP 04, (0001004)
	BNE CHECK_LOGO_COMMAND
	MOV HELP_COMMAND_MASK, R0
	MOV 0001010, R1
	CLR R2

CHECK_HELP_COMMAND_LOOP:
	CMP (R0)+, (R1)+
	BNE CHECK_LOGO_COMMAND
	INC R2
	CMP 04, R2
	BNE CHECK_HELP_COMMAND_LOOP
	JSR R7, HELP_COMMAND_FUNCTION
	BR CHECK_COMMAND_FUNCTION_END


CHECK_LOGO_COMMAND:
	# LOGO command check
	CMP 04, (0001004)
	BNE CHECK_CLR_COMMAND
	MOV LOGO_COMMAND_MASK, R0
	MOV 0001010, R1
	CLR R2

CHECK_LOGO_COMMAND_LOOP:
	CMP (R0)+, (R1)+
	BNE CHECK_COMMAND_FUNCTION_END
	INC R2
	CMP 04, R2
	BNE CHECK_LOGO_COMMAND_LOOP
	JSR R7, LOGO_COMMAND_FUNCTION
	BR CHECK_COMMAND_FUNCTION_END

CHECK_CLR_COMMAND:
	# CLR command check
	CMP 03, (0001004)
	BNE CHECK_COMMAND_FUNCTION_END
	MOV CLR_COMMAND_MASK, R0
	MOV 0001010, R1
	CLR R2

CHECK_CLR_COMMAND_LOOP:
	CMP (R0)+, (R1)+
	BNE CHECK_COMMAND_FUNCTION_END
	INC R2
	CMP 03, R2
	BNE CHECK_CLR_COMMAND_LOOP
	JSR R7, CLR_COMMAND_FUNCTION
	BR CHECK_COMMAND_FUNCTION_END

CHECK_COMMAND_FUNCTION_END:
	# Restore registers R0-R2
	MOV (R6)+, R2
	MOV (R6)+, R1
	MOV (R6)+, R0
	RST R7


# /-------------------------------------------------------\
# | "HALT" COMMAND FUNCTION                               |
# \-------------------------------------------------------/

HALT_COMMAND_FUNCTION:
	HALT
	RST R7


# /-------------------------------------------------------\
# | "HELP" COMMAND FUNCTION                              |
# \-------------------------------------------------------/

HELP_COMMAND_FUNCTION:
	# Save registers R0-R2
	MOV R0, -(R6)
	MOV R1, -(R6)
	MOV R2, -(R6)

	CLR R0
	MOV (HELP_MESSAGE_ADDRESS), R2
	MOV (R2)+, R1

HELP_COMMAND_FUNCTION_LOOP:
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	DEC R6
	MOVB (R2)+, -(R6)

	JSR R7, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6
	INC R0
	CMP R0, R1
	BEQ HELP_COMMAND_FUNCTION_PRE_END
	ADD 010, (0001000)
	CMP (0001000), (FRAME_WIDTH)
	BNE HELP_COMMAND_FUNCTION_LOOP
	MOV 000, (0001000)
	ADD 012, (0001002)

	# Check reaching bottom of the screen
	CMP (FRAME_HEIGHT), (0001002)
	BGT HELP_COMMAND_FUNCTION_LOOP

	JSR R7, CLEAR_SCREEN_FUNCTION
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
	CLR (0001000)
	CLR (0001002)
	CLR R0
	MOV (HELP_MESSAGE_ADDRESS), R2
	MOV (R2)+, R1
	BR HELP_COMMAND_FUNCTION_LOOP

HELP_COMMAND_FUNCTION_PRE_END:
	MOV 000, (0001000)
	ADD 012, (0001002)

HELP_COMMAND_FUNCTION_END:
	# Restore registers R0-R2
	MOV (R6)+, R2
	MOV (R6)+, R1
	MOV (R6)+, R0

	RST R7

# /-------------------------------------------------------\
# | "LOGO" COMMAND FUNCTION                               |
# \-------------------------------------------------------/

LOGO_COMMAND_FUNCTION:
	# Save registers R0-R4
	MOV R0, -(R6)
	MOV R1, -(R6)
	MOV R2, -(R6)
	MOV R3, -(R6)

LOGO_COMMAND_FUNCTION_AFTER_INIT:
	CLR (0001000)
	MOV (0001002), R0
	ADD 50, (0001002)
	CMP (FRAME_HEIGHT), (0001002)
	BGT LOGO_COMMAND_Y_POS_PRE_LOOP
	JSR R7, CLEAR_SCREEN_FUNCTION
	JSR R7, PRINT_GREETING_SYMBOL_FUNCTION
	BR LOGO_COMMAND_FUNCTION_AFTER_INIT

LOGO_COMMAND_Y_POS_PRE_LOOP:
	CLR R1
	MOV (FRAME_BUFFER_OFFSET), R2

LOGO_COMMAND_Y_POS_LOOP:
	CMP R1, R0
	BEQ LOGO_COMMAND_CONFIGURE_REGISTERS
	INC R1
	ADD (FRAME_WIDTH_BYTES), R2
	BR LOGO_COMMAND_Y_POS_LOOP

LOGO_COMMAND_CONFIGURE_REGISTERS:
	MOV (LOGO_ADDRESS), R0
	MOV 5000, R1
	CLR R3

LOGO_COMMAND_LOOP:
	MOVB (R0)+, (R2)+
	INC R3

	CMP R3, R1
	BEQ LOGO_COMMAND_END
	BR LOGO_COMMAND_LOOP

LOGO_COMMAND_END:

	MOV (R6)+, R3
	MOV (R6)+, R2
	MOV (R6)+, R1
	MOV (R6)+, R0
	RST R7

# /-------------------------------------------------------\
# | "CLR" COMMAND FUNCTION                                |
# \-------------------------------------------------------/

CLR_COMMAND_FUNCTION:
	JSR R7, CLEAR_SCREEN_FUNCTION
	RST R7

# /-------------------------------------------------------\
# | PRINT GREETING SYMBOL FUNCTION                        |
# \-------------------------------------------------------/
PRINT_GREETING_SYMBOL_FUNCTION:
	MOV (0001000), -(R6)
	MOV (0001002), -(R6)
	MOV 0000105, -(R6)
	JSR R7, PRINT_SCREEN_CHAR_FUNCTION
	ADD 6, R6
	ADD 010, (0001000)
	RST R7

# /-------------------------------------------------------\
# | CLEAR SCREEN FUNCTION                                 |
# \-------------------------------------------------------/
CLEAR_SCREEN_FUNCTION:
	MOV R0, -(R6)
	MOV R1, -(R6)
	MOV (FRAME_BUFFER_OFFSET), R0
	CLR R1

CLEAR_SCREEN_FUNCTION_LOOP:
	CMP R1, FRAME_BUFFER_SIZE
	BLT CLEAR_SCREEN_FUNCTION_END
	CLR (R0)+
	
	ADD 2, R1
	
	BR CLEAR_SCREEN_FUNCTION_LOOP

CLEAR_SCREEN_FUNCTION_END:
	CLR (0001000)
	CLR (0001002)
	CLR (0001004)
	MOV 0001010, (0001006)

	# JSR R7, PRINT_GREETING_SYMBOL_FUNCTION

	MOV (R6)+, R1
	MOV (R6)+, R0
	RST R7


# /-------------------------------------------------------\
# | CONVERT KEYBOARD CODE TO FONT CODE FUNCTION           |
# \-------------------------------------------------------/
KEYBOARD_TO_FONT_CONVERT_FUNCTION:
	# Operands: keycode SP+2
	# Result: fontcode SP+2
	MOV R0, -(R6)
	MOV 4(R6), R0
	BIT 0000100, R0
	BEQ KEYBOARD_TO_FONT_CONVERT_LOWER

	BIC 0177700, R0
	ASL R0
	ADD (KEYBOARD_TO_FONT_TABLE_ADDRESS), R0
	MOV @(R0), R0
	SWAB R0
	BIC 0177400, R0

	MOV R0, 4(R6)
	MOV (R6)+, R0
	RST R7

KEYBOARD_TO_FONT_CONVERT_LOWER:
	BIC 0177700, R0
	ASL R0
	ADD (KEYBOARD_TO_FONT_TABLE_ADDRESS), R0
	MOV @(R0), R0
	BIC 0177400, R0

	MOV R0, 4(R6)
	MOV (R6)+, R0
	RST R7

# /-------------------------------------------------------\
# | PRINT SCREEN CHARACTER FUNCTION                       |
# \-------------------------------------------------------/
PRINT_SCREEN_CHAR_FUNCTION:
	# Operands: x, y, char_code
	# Old R5 value: SP+0
	# char_code: SP+2
	# y: SP+4
	# x: SP+6

	# Save registers R1-R5
	MOV R1, -(R6)
	MOV R2, -(R6)
	MOV R3, -(R6)
	MOV R4, -(R6)
	MOV R5, -(R6)

	# Variables:
	# Old Reg value: SP+12
	# tmp: R5
	# vram_line_offset:  R4
	# line_printed: R3
	# char_code: R2
	# y: R1

	MOV 12(R6), R2
	MOV 14(R6), R1
	# line_printed
	CLR R3
	# tmp
	CLR R5

	# vram_line_offset
	MOV 16(R6), R4
	ASR R4
	ASR R4
	ADD (FRAME_BUFFER_OFFSET), R4
	

PRINT_SCREEN_CHAR_H_OFFSET_LOOP:
	CMP R5, R1
	BEQ PRINT_SCREEN_CHAR_LOAD_SYMBOL
	ADD (FRAME_WIDTH_BYTES), R4
	INC R5
	BR PRINT_SCREEN_CHAR_H_OFFSET_LOOP

PRINT_SCREEN_CHAR_LOAD_SYMBOL:
	MOV (FONT_ADDRESS), R5

PRINT_SCREEN_CHAR_LOAD_SYMBOL_LOOP:
	CMP R3, R2
	BEQ PRINT_SCREEN_CHAR_RENDERING
	ADD (SYMBOL_IMAGE_SIZE), R5
	INC R3
	BR PRINT_SCREEN_CHAR_LOAD_SYMBOL_LOOP

PRINT_SCREEN_CHAR_RENDERING:
	CLR R3

PRINT_SCREEN_CHAR_RENDERING_LOOP:
	CMP R3, 8
	BEQ PRINT_SCREEN_CHAR_END
	MOV @(R5), @(R4)
	ADD (FRAME_WIDTH_BYTES), R4
	INC R3
	INC R5
	INC R5
	BR PRINT_SCREEN_CHAR_RENDERING_LOOP

PRINT_SCREEN_CHAR_END:

	# Restore registers R1-R5
	MOV (R6)+, R5
	MOV (R6)+, R4
	MOV (R6)+, R3
	MOV (R6)+, R2
	MOV (R6)+, R1

	RST R7
